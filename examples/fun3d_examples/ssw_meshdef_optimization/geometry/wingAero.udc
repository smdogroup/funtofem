# Generate wing CFD component

# Thickness parameters (thickness/chord)
interface tc1 in 1
interface tc2 in 1
interface tc3 in 1
interface tc4 in 1

# Rectangular wing, no sweep
interface chord in 1
interface sspan in 5.0
interface camber in 0.0
interface xCamber in 0.4
interface aoa in 0.0

# Geometric twist parameters
interface twist1 in 1
interface twist2 in 1
interface twist3 in 1
interface twist4 in 1

interface sharpTE in 1
interface ff in 20.0

# Make the symmetry cut box
box -2*ff -2*ff 0 4*ff 4*ff -4*ff
store sym_cut_box

# Make the full wing solid
udparg $/wingSolid tc1 tc1 tc2 tc2 tc3 tc3 tc4 tc4
udparg $/wingSolid twist1 twist1 twist2 twist2 twist3 twist3 twist4 twist4
udparg $/wingSolid chord chord sspan sspan camber camber xCamber xCamber
udparg $/wingSolid chopMode 0
udprim $/wingSolid midplane 0 wingCap 1
store wingSolid

box -ff -ff -ff 2*ff 2*ff 2*ff
   attribute capsGroup $farfield
   attribute capsMesh $farfieldMesh
   attribute AFLR_GBC $FARFIELD_UG3_GBC
   attribute AFLR4_CMP_ID 1
   attribute AFLR4_Scale_Factor 10.0

restore wingSolid
select face
   attribute capsGroup $wing
   attribute capsMesh $wingMesh
   attribute AFLR4_CMP_ID 3
   attribute AFLR4_Edge_Refinement_Weight 0.1
   attribute AFLR_GBC $-STD_UG3_GBC

subtract

restore sym_cut_box
intersect

select edge $tagComp1 $leEdge
   attribute capsMesh $leEdgeMesh

select edge $tagComp1 $teEdge
   attribute capsMesh $teEdgeMesh

select face $capsGroup $*
select not
   attribute capsGroup $symmetry
   attribute capsMesh $symmetryMesh
   attribute AFLR4_CMP_ID 2
   attribute AFLR_GBC $BL_INT_UG3_GBC

udprim editAttr filename <<
   EDGE ADJ2FACE tagComp1=wingUpper
   AND ADJ2FACE capsGroup=symmetry
   SET capsMesh=rootUpperEdgeMesh

   EDGE ADJ2FACE tagComp1=wingLower
   AND ADJ2FACE capsGroup=symmetry
   SET capsMesh=rootLowerEdgeMesh

   EDGE ADJ2FACE tagComp1=wingUpper
   AND ADJ2FACE tagComp=tip
   SET capsMesh=tipUpperEdgeMesh

   EDGE ADJ2FACE tagComp1=wingLower
   AND ADJ2FACE tagComp=tip
   SET capsMesh=tipLowerEdgeMesh
>>

select body
   attribute capsAIM $fun3dAIM;aflr4AIM;aflr3AIM;egadsTessAIM
   attribute capsMeshLength 1.0

rotatex 90 0 0
rotatez -aoa 0 0

end
||||||||