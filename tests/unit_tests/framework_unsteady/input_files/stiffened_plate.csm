cfgpmtr nx 3
cfgpmtr ny 3

patbeg ix 3
   patbeg iy 3
      box (ix-1)/3 (iy-1)/3 0 1/3 1/3 0
      set ipanel 3*ix+iy
      select face
       attribute capsGroup !$panel+ipanel
        attribute _color $blue
       attribute panelx !$panel+ix
       attribute panely !$panel+iy
      ifthen ix EQ 1
         select edge 1
         attribute capsConstraint $fix
      endif
      ifthen ix EQ nx
         select edge 3
         attribute capsConstraint $fix
      endif
      ifthen iy EQ 1
         select edge 2
         attribute capsConstraint $fix
      endif
      ifthen iy EQ ny
         select edge 4
         attribute capsConstraint $fix
      endif
   patend
patend
patbeg foo 8
   union
patend

set midx (nx+1)/2
set midy (ny+1)/2
udprim editAttr filename <<
  EDGE ADJ2FACE panelx=!$panel+(midx-1)
    AND ADJ2FACE panelx=!$panel+midx
    AND ADJ2FACE panely=!$panel+midy
    SET capsConstraint=temp
  EDGE ADJ2FACE panelx=!$panel+midx
    AND ADJ2FACE panelx=!$panel+(midx+1)
    AND ADJ2FACE panely=!$panel+midy
    SET capsConstraint=temp
  EDGE ADJ2FACE panely=!$panel+(midy-1)
    AND ADJ2FACE panely=!$panel+midy
    AND ADJ2FACE panelx=!$panel+midx
    SET capsConstraint=temp
  EDGE ADJ2FACE panely=!$panel+midy
    AND ADJ2FACE panely=!$panel+(midy+1)
    AND ADJ2FACE panelx=!$panel+midx
    SET capsConstraint=temp

  NODE ADJ2EDGE capsConstraint=fix
  SET capsConstraint=fix
  NODE ADJ2EDGE capsConstraint=temp
  SET capsConstraint=temp
>>

select body
attribute capsAIM $egadsTessAIM;tacsAIM

end
||||||||||||||||||||||